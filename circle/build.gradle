
apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply from: '../android.gradle'

def buildInfo() {
    if ('git status --short'.execute().text.trim().length() == 0)
        return 'git rev-parse --short HEAD'.execute().text.trim() + "-" + userName()
    else
        return "private-" + userName()
}

def userName() {
    return System.properties["user.name"]
}

def genVersionName(){
    return VERSION_NAME +"-" + buildInfo()
}

def genVersionCode(){
    return VERSION_CODE.toInteger()
}

android {
    defaultConfig {
        applicationId "com.jadenine.circle"
        versionCode genVersionCode()
        versionName genVersionName()

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    def CHANNELS = ["Jadenine"]

    productFlavors {
        CHANNELS.each { name ->
            "$name" {
                resValue "string", "umeng_channel", name
            }
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.outputFile = new File(
                    output.outputFile.parent,
                    output.outputFile.name.replace(".apk", "-"+getVersionName()+".apk"))
        }
    }
    publishNonDefault false
}

dependencies {
    apt 'com.google.dagger:dagger-compiler:2.0'
    provided 'org.glassfish:javax.annotation:10.0-b28'

    releaseCompile project(path: ':domain', configuration: 'release')
    debugCompile project(path: ':domain', configuration: 'debug')
    releaseCompile project(path: ':common', configuration: 'release')
    debugCompile project(path: ':common', configuration: 'debug')

    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:22.2.0'
    compile 'com.android.support:design:22.2.0'
    compile 'com.android.support:recyclerview-v7:22.2.0'
    compile 'com.android.support:cardview-v7:22.2.0'
    compile 'com.android.support:palette-v7:22.2.0'

    compile 'com.squareup:otto:1.3.7'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.squareup.okhttp:okhttp:2.3.0'

    compile 'com.jakewharton:butterknife:6.1.0'
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.3.1'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.3.1'

    compile 'com.umeng.analytics:analytics:5.5.3'

    androidTestCompile 'com.android.support:support-annotations:22.2.0'
    androidTestCompile 'com.android.support.test:runner:0.3'
    androidTestCompile 'com.android.support.test:rules:0.3'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2'
    androidTestCompile 'com.android.support.test.espresso:espresso-contrib:2.2'
    androidTestCompile 'com.android.support.test.espresso:espresso-intents:2.2'
}
